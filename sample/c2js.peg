//-------------------------------------------------------------------------
//  A.2.4  External definitions
//-------------------------------------------------------------------------

TranslationUnit
    = << Spacing ExternalDeclaration@+ EOT #translationunit >>
    ;

ExternalDeclaration
    = FunctionDefinition
    / Declaration
    ;

FunctionDefinition
    = << DeclarationSpecifiers@ Declarator@ DeclarationList@? CompoundStatement@ #functiondefinition>>
    ;

DeclarationList
    = << Declaration@+ #declarationlist>>
    ;


//-------------------------------------------------------------------------
//  A.2.2  Declarations
//-------------------------------------------------------------------------

Declaration
    = << DeclarationNoAttribute@ (Attribute / Assembler)@? SEMI #declaration >>
    ;

DeclarationNoAttribute
    = DeclarationSpecifiers@ InitDeclaratorList@?
    ;

DeclarationSpecifiers
    = << ( StorageClassSpecifier
       / TypeQualifier
       / FunctionSpecifier
       / Attribute
       )@*
       TypedefName@
       ( StorageClassSpecifier
       / TypeQualifier
       / FunctionSpecifier
       / Attribute
       )* #declarationspecifier >>
    / << ( StorageClassSpecifier
      / TypeSpecifier
      / TypeQualifier
      / FunctionSpecifier
      / Attribute
      )@+ #declarationspecifier >>
    ;

InitDeclaratorList
    = InitDeclarator <<@ (COMMA InitDeclarator@) #initdeclaratorlist >>*
    ;

InitDeclarator
    = Declarator <<@ (EQU Initializer@) #initdeclarator >>?
    ;

StorageClassSpecifier
    = << (TYPEDEF
    / EXTERN
    / STATIC
    / AUTO
    / REGISTER) #storageclassspecifier >>
    ;

TypeSpecifier
    = << (VOID
    / CHAR
    / SHORT
    / INT
    / LONG
    / FLOAT
    / DOUBLE
    / SIGNED
    / UNSIGNED
    / BOOL
    / COMPLEX
    / StructOrUnionSpecifier
    / EnumSpecifier)@ #typespecifier >>
    ;

StructOrUnionSpecifier
    = StructOrUnion@
      ( Identifier@? LWING StructDeclaration@+ RWING
      / Identifier@
      )
    ;

StructOrUnion
    = << STRUCT #struct >>
    / << UNION #union >>
    ;

StructDeclaration
    = << SpecifierQualifierList StructDeclaratorList@? SEMI #structdeclaration >>
    ;

SpecifierQualifierList
    = ( TypeQualifier*
        TypedefName
        TypeQualifier*
      )@
    / ( TypeSpecifier
      / TypeQualifier
      )@+
    ;

StructDeclaratorList
    = << StructDeclarator (COMMA StructDeclarator)* #structdeclaratorlist >>
    ;

StructDeclarator
    = Declarator@? COLON ConstantExpression@
    / Declarator@
    ;

EnumSpecifier
    = ENUM
      ( Identifier@? LWING EnumeratorList@ COMMA? RWING
      / Identifier@
      )
    ;

EnumeratorList
    = Enumerator <<@ (COMMA Enumerator@)* #enumeratorlist >>
    ;

Enumerator
    = EnumerationConstant (EQU ConstantExpression)?
    ;

TypeQualifier
    = << CONST #const>>
    / RESTRICT
    / VOLATILE
    / DECLSPEC LPAR Identifier RPAR
    ;

FunctionSpecifier
    = << INLINE #inline >>
    / << STDCALL #stdcall >>
    ;

Attribute
    = << ATTRIBUTE #attribute LPAR LPAR (PostfixExpression@ / TypeQualifier@ / Identifier@) AttributeParamList? RPAR RPAR >>;

AttributeParam = << IntegerConstant@ / Identifier@ / StringLiteral@ #attributeparam >>;

AttributeParamList = LPAR (AttributeParam@ (COMMA AttributeParam@)*)? RPAR;


Assembler = ASM LPAR (StringLiteral (COMMA StringLiteral)*)? RPAR;

Declarator
    = << Pointer@? DirectDeclarator@ #declarator >>
    ;

DirectDeclarator
    = ( Identifier@
      / LPAR Declarator@ RPAR
      )
      ( LBRK TypeQualifier@* AssignmentExpression@? RBRK
      / LBRK STATIC TypeQualifier@* AssignmentExpression@ RBRK
      / LBRK TypeQualifier@+ STATIC AssignmentExpression@ RBRK
      / LBRK TypeQualifier@* STAR RBRK
      / LPAR ParameterTypeList RPAR
      / LPAR IdentifierList@? RPAR
      )*
    ;

Pointer
    = << ( STAR TypeQualifier@* )+ #pointer >>
    ;

ParameterTypeList
    = ParameterList@ (COMMA ELLIPSIS)?
    ;

ParameterList
    = ParameterDeclaration <<@ (COMMA ParameterDeclaration@) #parameterlist>>*
    ;

ParameterDeclaration
    = DeclarationSpecifiers <<@ ( Declarator / AbstractDeclarator )@ #parameterdeclaration >>?
    ;

IdentifierList
    = << Identifier@ (COMMA Identifier@)* #identifierlist >>
    ;

TypeName
    = << SpecifierQualifierList@ AbstractDeclarator@? #typename >>
    ;

AbstractDeclarator
    = Pointer? DirectAbstractDeclarator
    / Pointer
    ;

DirectAbstractDeclarator
    = ( LPAR AbstractDeclarator RPAR
      / LBRK (AssignmentExpression / STAR)? RBRK
      / LPAR ParameterTypeList? RPAR
      )
      ( LBRK (AssignmentExpression / STAR)? RBRK
      / LPAR ParameterTypeList? RPAR
      )*
    ;

TypedefName
    = << Identifier@ #typedefname >>
    ;

Initializer
    = << (AssignmentExpression@
    / LWING InitializerList@* COMMA? RWING) #initializer>>
    ;

InitializerList
    = << Designation@? Initializer@ (COMMA Designation@? Initializer@)* #initializerlist >>
    ;

Designation
    = << Designator@+ EQU #designation>>
    ;

Designator
    = LBRK ConstantExpression RBRK
    / DOT Identifier
    ;


//-------------------------------------------------------------------------
//  A.2.3  Statements
//-------------------------------------------------------------------------

Statement
    = LabeledStatement
    / CompoundStatement
    / ExpressionStatement
    / SelectionStatement
    / IterationStatement
    / JumpStatement
    ;

LabeledStatement
    = Identifier COLON Statement
    / << CASE ConstantExpression@ COLON Statement@ #case >>
    / << DEFAULT COLON Statement@ #default >>
    ;

CompoundStatement
    = << LWING ( Declaration / Statement )@* RWING #compoundstatement >>
    ;

ExpressionStatement
    = << Expression@? SEMI #expressionstatement >>
    ;

SelectionStatement
    = << IF LPAR Expression@ RPAR Statement@ (ELSE Statement@)? #ifstatement >>
    / << SWITCH LPAR Expression@ RPAR Statement@ #switchstatement >>
    ;

IterationStatement
    = << WHILE LPAR Expression@ RPAR Statement@ #whilestatement >>
    / << DO Statement@ WHILE LPAR Expression@ RPAR SEMI #dowhilestatement >>
    / << FOR LPAR Expression@? SEMI Expression@? SEMI Expression@? RPAR Statement@ #forstatement >>
    / << FOR LPAR Declaration@ Expression@? SEMI Expression@? RPAR Statement@ #forstatement >>
    ;

JumpStatement
    = GOTO Identifier SEMI
    / << CONTINUE SEMI #continue>>
    / << BREAK SEMI #break >>
    / << RETURN Expression@? SEMI #returnstatement >>
    ;


//-------------------------------------------------------------------------
//  A.2.1  Expressions
//-------------------------------------------------------------------------

PrimaryExpression
    = Identifier
    / Constant
    / StringLiteral
    / LPAR Expression RPAR
    ;

Index
    = LBRK Expression@ RBRK
    ;


FunctionCall
    = LPAR ArgumentExpressionList@? RPAR
    ;

PostfixExpression
    = ( PrimaryExpression
      / LPAR TypeName RPAR LWING InitializerList COMMA? RWING
      )
      <<@
      ( FunctionCall #apply
      / Index #index
      / DOT #field Identifier@
      / PTR #ptr Identifier@
      / INC #inc
      / DEC #dec
      )@ >>*
    ;

ArgumentExpressionList
    = AssignmentExpression <<@ (COMMA AssignmentExpression@) #argumentexpressionlist >>*
    ;

UnaryExpression
    = PostfixExpression
    / INC UnaryExpression
    / DEC UnaryExpression
    / UnaryOperator CastExpression
    / << SIZEOF #sizeof (UnaryExpression@ / LPAR TypeName@ RPAR ) >>
    ;

UnaryOperator
    = AND
    / STAR
    / PLUS
    / MINUS
    / TILDA
    / BANG
    ;

CastExpression
    = <<(LPAR TypeName RPAR)@+ UnaryExpression@ #castexpression >>
    / UnaryExpression
    ;

MultiplicativeExpression
    = CastExpression <<@ (STAR #star / DIV #div / MOD #mod ) CastExpression@ >>*
    ;

AdditiveExpression
    = MultiplicativeExpression <<@ (PLUS #plus / MINUS #minus) MultiplicativeExpression@ >>*
    ;

ShiftExpression
    = AdditiveExpression <<@ (LEFT #left / RIGHT #right) AdditiveExpression@ >>*
    ;

RelationalExpression
    = ShiftExpression <<@  (LE #le / GE #ge / LT #lt / GT #gt) ShiftExpression@ >>*
    ;

EqualityExpression
    = RelationalExpression <<@ (EQUEQU #equequ /BANGEQU #bangequ) RelationalExpression@ >>*
    ;

ANDExpression
    = EqualityExpression <<@ (AND #and) EqualityExpression@ >>*
    ;

ExclusiveORExpression
    = ANDExpression <<@ (HAT #hat) ANDExpression@ >>*
    ;

InclusiveORExpression
    = ExclusiveORExpression <<@ (OR #or) ExclusiveORExpression@ >>*
    ;

LogicalANDExpression
    = InclusiveORExpression <<@ (ANDAND #andand) InclusiveORExpression@ >>*
    ;

LogicalORExpression
    = LogicalANDExpression <<@ (OROR #oror) LogicalANDExpression@ >>*
    ;

ConditionalExpression
    = LogicalORExpression <<@ (QUERY #query) Expression@ COLON LogicalORExpression@ >>*
    ;

AssignmentExpression
    = << UnaryExpression@ AssignmentOperator@ AssignmentExpression@ #assignmentexpression >>
    / ConditionalExpression
    ;

AssignmentOperator
    = << (EQU
    / STAREQU
    / DIVEQU
    / MODEQU
    / PLUSEQU
    / MINUSEQU
    / LEFTEQU
    / RIGHTEQU
    / ANDEQU
    / HATEQU
    / OREQU) #assignmentoperator >>
    ;

Expression
    = AssignmentExpression <<@ (COMMA AssignmentExpression@) #expression >>*
    ;

ConstantExpression
    = ConditionalExpression
    ;


//-------------------------------------------------------------------------
//  A.1.1  Lexical elements
//  Tokens are: Keyword, Identifier, Constant, StringLiteral, Punctuator.
//  Tokens are separated by Spacing.
//-------------------------------------------------------------------------

Spacing
    = ( WhiteSpace
      / LongComment
      / LineComment
      / Pragma
      )*
    ;

WhiteSpace  = [ \n\r\t\u000B\u000C] ;

LongComment = "/*" (!"*/" .)* "*/" ;

LineComment = "//" (!"\n" .)* ;

Pragma      = "#"  (!"\n" .)* ;


//-------------------------------------------------------------------------
//  A.1.2  Keywords
//-------------------------------------------------------------------------

AUTO      = "auto"       !IdChar Spacing ;
BREAK     = "break"      !IdChar Spacing ;
CASE      = "case"       !IdChar Spacing ;
CHAR      = "char"       !IdChar Spacing ;
CONST     = "const"      !IdChar Spacing ;
CONTINUE  = "continue"   !IdChar Spacing ;
DEFAULT   = "default"    !IdChar Spacing ;
DOUBLE    = "double"     !IdChar Spacing ;
DO        = "do"         !IdChar Spacing ;
ELSE      = "else"       !IdChar Spacing ;
ENUM      = "enum"       !IdChar Spacing ;
EXTERN    = "extern"     !IdChar Spacing ;
FLOAT     = "float"      !IdChar Spacing ;
FOR       = "for"        !IdChar Spacing ;
GOTO      = "goto"       !IdChar Spacing ;
IF        = "if"         !IdChar Spacing ;
INT       = "int"        !IdChar Spacing ;
INLINE    = ("inline" / "__inline")    !IdChar Spacing ;
LONG      = "long"       !IdChar Spacing ;
REGISTER  = "register"   !IdChar Spacing ;
RESTRICT  = "restrict"   !IdChar Spacing ;
RETURN    = "return"     !IdChar Spacing ;
SHORT     = "short"      !IdChar Spacing ;
SIGNED    = "signed"     !IdChar Spacing ;
SIZEOF    = "sizeof"     !IdChar Spacing ;
STATIC    = "static"     !IdChar Spacing ;
STRUCT    = "struct"     !IdChar Spacing ;
SWITCH    = "switch"     !IdChar Spacing ;
TYPEDEF   = "typedef"    !IdChar Spacing ;
UNION     = "union"      !IdChar Spacing ;
UNSIGNED  = "unsigned"   !IdChar Spacing ;
VOID      = "void"       !IdChar Spacing ;
VOLATILE  = "volatile"   !IdChar Spacing ;
WHILE     = "while"      !IdChar Spacing ;
BOOL      = "_Bool"      !IdChar Spacing ;
COMPLEX   = "_Complex"   !IdChar Spacing ;
STDCALL   = "_stdcall"   !IdChar Spacing ;
DECLSPEC  = "__declspec" !IdChar Spacing ;
ATTRIBUTE = "__attribute__" !IdChar Spacing ;
ASM       = "__asm" !IdChar Spacing ;

Keyword
    = ( "auto"
      / "break"
      / "case"
      / "char"
      / "const"
      / "continue"
      / "default"
      / "double"
      / "do"
      / "else"
      / "enum"
      / "extern"
      / "float"
      / "for"
      / "goto"
      / "if"
      / "int"
      / "inline"
      / "long"
      / "register"
      / "restrict"
      / "return"
      / "short"
      / "signed"
      / "sizeof"
      / "static"
      / "struct"
      / "switch"
      / "typedef"
      / "union"
      / "unsigned"
      / "void"
      / "volatile"
      / "while"
      / "_Bool"
      / "_Complex"
      / "_Imaginary"
      / "_stdcall"
      / "__declspec"
      / "__attribute__"
      / "__asm"
      )
    !IdChar ;


//-------------------------------------------------------------------------
//  A.1.3  Identifiers
//  The standard does not explicitly state that identifiers must be
//  distinct from keywords, but it seems so.
//-------------------------------------------------------------------------

Identifier = << !Keyword IdNondigit IdChar* Spacing #identifier >>;

IdNondigit
    = [a-z] / [A-Z] / [_]
    / UniversalCharacter
    ;

IdChar
    = [a-z] / [A-Z] / [0-9] / [_]
    / UniversalCharacter
    ;


//-------------------------------------------------------------------------
//  A.1.4  Universal character names
//-------------------------------------------------------------------------

UniversalCharacter
    = "\\u" HexQuad
    / "\\U" HexQuad HexQuad
    ;

HexQuad = HexDigit HexDigit HexDigit HexDigit ;


//-------------------------------------------------------------------------
//  A.1.5  Constants
//-------------------------------------------------------------------------

Constant
    = FloatConstant
    / IntegerConstant       // Note: can be a prefix of Float Constant!
    / EnumerationConstant
    / CharacterConstant
    ;

IntegerConstant
    = << ( DecimalConstant
      / HexConstant
      / OctalConstant
      )
    IntegerSuffix? #integer >> Spacing
    ;

DecimalConstant = [1-9][0-9]* ;

OctalConstant   = "0"[0-7]* ;

HexConstant     = HexPrefix HexDigit+ ;

HexPrefix       = "0x" / "0X" ;

HexDigit        = [a-f] / [A-F] / [0-9] ;

IntegerSuffix
    = [uU] Lsuffix?
    / Lsuffix [uU]?
    ;

Lsuffix
    = "ll"
    / "LL"
    / [lL]
    ;

FloatConstant
    = << ( DecimalFloatConstant
      / HexFloatConstant
      )
    FloatSuffix? #float >> Spacing
    ;

DecimalFloatConstant
    = Fraction Exponent?
    / [0-9]+ Exponent
    ;

HexFloatConstant
    = HexPrefix HexFraction BinaryExponent?
    / HexPrefix HexDigit+ BinaryExponent
    ;

Fraction
    = [0-9]* "." [0-9]+
    / [0-9]+ "."
    ;

HexFraction
    = HexDigit* "." HexDigit+
    / HexDigit+ "."
    ;

Exponent = [eE][+\-]? [0-9]+ ;

BinaryExponent = [pP][+\-]? [0-9]+ ;

FloatSuffix = [flFL] ;

EnumerationConstant = Identifier ;

CharacterConstant = << "L"? "'" Char* "'" Spacing #characterconstant >>;

Char = Escape / !['\n\\] . ;

Escape
    = SimpleEscape
    / OctalEscape
    / HexEscape
    / UniversalCharacter
    ;

SimpleEscape = "\\" ['\"?\\abfnrtv] ;
OctalEscape  = "\\" [0-7][0-7]?[0-7]? ;
HexEscape    = "\\x" HexDigit+ ;


//-------------------------------------------------------------------------
//  A.1.6  String Literals
//-------------------------------------------------------------------------

StringLiteral = << "L"? (["] StringChar* ["] Spacing)+ #string >>;
// FIXME we need to concatinate StringLiterals
// e.g.) "a" "b" => "ab"

StringChar = Escape / ![\"\n\\] . ;


//-------------------------------------------------------------------------
//  A.1.7  Punctuators
//-------------------------------------------------------------------------

LBRK       =  "["         Spacing ;
RBRK       =  "]"         Spacing ;
LPAR       =  "("         Spacing ;
RPAR       =  ")"         Spacing ;
LWING      =  "{"         Spacing ;
RWING      =  "}"         Spacing ;
DOT        =  "."         Spacing ;
PTR        =  "->"        Spacing ;
INC        =  "++"        Spacing ;
DEC        =  "--"        Spacing ;
AND        =  "&"  ![&]   Spacing ;
STAR       =  "*"  ![=]   Spacing ;
PLUS       =  "+"  ![+=]  Spacing ;
MINUS      =  "-"  ![\-=>]Spacing ;
TILDA      =  "~"         Spacing ;
BANG       =  "!"  ![=]   Spacing ;
DIV        =  "/"  ![=]   Spacing ;
MOD        =  "%"  ![=>]  Spacing ;
LEFT       =  "<<" ![=]   Spacing ;
RIGHT      =  ">>" ![=]   Spacing ;
LT         =  "<"  ![=]   Spacing ;
GT         =  ">"  ![=]   Spacing ;
LE         =  "<="        Spacing ;
GE         =  ">="        Spacing ;
EQUEQU     =  "=="        Spacing ;
BANGEQU    =  "!="        Spacing ;
HAT        =  "^"  ![=]   Spacing ;
OR         =  "|"  ![=]   Spacing ;
ANDAND     =  "&&"        Spacing ;
OROR       =  "||"        Spacing ;
QUERY      =  "?"         Spacing ;
COLON      =  ":"  ![>]   Spacing ;
SEMI       =  ";"         Spacing ;
ELLIPSIS   =  "..."       Spacing ;
EQU        =  "="  !"="   Spacing ;
STAREQU    =  "*="        Spacing ;
DIVEQU     =  "/="        Spacing ;
MODEQU     =  "%="        Spacing ;
PLUSEQU    =  "+="        Spacing ;
MINUSEQU   =  "-="        Spacing ;
LEFTEQU    =  "<<="       Spacing ;
RIGHTEQU   =  ">>="       Spacing ;
ANDEQU     =  "&="        Spacing ;
HATEQU     =  "^="        Spacing ;
OREQU      =  "|="        Spacing ;
COMMA      =  ","         Spacing ;

EOT        = !. ;

TopLevel
    = TranslationUnit
    ;
